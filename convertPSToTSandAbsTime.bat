@echo off
:: Converting the container using FFMPEG and setting absolute time (rather than relative time) via MPEGTool.
SET CONTAINER_EXTN=mpg
:: Recursively search (current and subfolders) for filenames of media containers; "delims=" means do
:: not parse into tokens (preserve each line). Thus, each media container filename is passed into the
:: %%f variable of the ffmpegConvert function.
for /f "delims=" %%f in ('dir "*.%CONTAINER_EXTN%" /s/b') do @call:ffmpegConvert "%%f"
goto:eof

:ffmpegConvert
SET THE_ORIGINAL_NAME="%~f1"
:: Grab different parts of the filename passed to this ffmpegConvert function
SET THE_DRIVE_LETTER=%~d1
SET THE_PATH=%~p1
SET THE_FILE=%~n1
SET THE_EXTN=%~x1
:: Create a backup of the original video for FFMPEG
SET THE_FFMPEG_BACKUP_NAME="%THE_DRIVE_LETTER%%THE_PATH%%THE_FILE%.deleteme"
move /Y %THE_ORIGINAL_NAME% %THE_FFMPEG_BACKUP_NAME%

:: Use FFMPEG to reconstruct the container
ffmpeg -fflags +genpts -i %THE_FFMPEG_BACKUP_NAME% -c:v copy -c:a copy -f mpegts %THE_ORIGINAL_NAME%

:: Get default name MPEGTool.exe will generate for the backup video file (it does this automatically)
SET THE_MPEGTOOL_BACKUP_NAME="%THE_DRIVE_LETTER%%THE_PATH%%THE_FILE%.mpg.bak"
:: Set absolute time via MPEGTool (the absolute time is based on the timestamp in the filename)
MPEGTool -i "%THE_ORIGINAL_NAME%" -nodisplay -filetime

:: Delete the corrupt original file (fixed by FFMPEG) to save space
del /F /Q %THE_FFMPEG_BACKUP_NAME%
:: Delete the backup file generated by MPEGTool.exe
del /F /Q %THE_MPEGTOOL_BACKUP_NAME%
goto:eof
